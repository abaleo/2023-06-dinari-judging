kutugu

medium

# BuyOrderIssuer / SellOrderProcessor feesEarned calculation has precision errors

## Summary

In BuyOrderIssuer / SellOrderProcessor, feeEarned is calculated by multiplying the amount of fillOrder per time by percentageFee. Due to precision errors in division and scaling is not used here, so every time the order is filled, there will be a rounding error in the fee calculation result.  
When there are a large number of low quantity buyOrders in the market, the calculation results of fee can even be 0, which will affect the agreement income.

## Vulnerability Detail

```solidity
    function _fillOrderAccounting(
        OrderRequest calldata orderRequest,
        bytes32 orderId,
        OrderState memory orderState,
        uint256 fillAmount,
        uint256 receivedAmount
    ) internal virtual override {
        // Accumulate fee obligations at each sill then take all at end
        uint256 collection = getPercentageFeeForOrder(receivedAmount);
        uint256 feesEarned = _feesEarned[orderId] + collection;
        // If order completely filled, clear fee data
        uint256 remainingOrder = orderState.remainingOrder - fillAmount;
        if (remainingOrder == 0) {
            // Clear fee state
            delete _feesEarned[orderId];
        } else {
            // Update fee state with earned fees
            if (collection > 0) {
                _feesEarned[orderId] = feesEarned;
            }
        }

        // Burn asset
        IMintBurn(orderRequest.assetToken).burn(fillAmount);
        // Transfer raw proceeds of sale here
        IERC20(orderRequest.paymentToken).safeTransferFrom(msg.sender, address(this), receivedAmount);
        // Distribute if order completely filled
        if (remainingOrder == 0) {
            _distributeProceeds(
                orderRequest.paymentToken, orderRequest.recipient, orderState.received + receivedAmount, feesEarned
            );
        }
    }

    function _cancelOrderAccounting(OrderRequest calldata orderRequest, bytes32 orderId, OrderState memory orderState)
        internal
        virtual
        override
    {
        // If no fills, then full refund
        uint256 refund;
        if (orderState.remainingOrder == orderRequest.quantityIn) {
            // Full refund
            refund = orderRequest.quantityIn;
        } else {
            // Otherwise distribute proceeds, take accumulated fees, and refund remaining order
            _distributeProceeds(
                orderRequest.paymentToken, orderRequest.recipient, orderState.received, _feesEarned[orderId]
            );
            // Partial refund
            refund = orderState.remainingOrder;
        }

        // Clear fee data
        delete _feesEarned[orderId];

        // Return escrow
        IERC20(orderRequest.assetToken).safeTransfer(orderRequest.recipient, refund);
    }
```
Above code is how to accumulate fee when fillOrder: "Accumulate fee obligations at each sill then take all at end", and the feesEarned is allocated to the treasury if order is done or canceled. Due to precision error, it is lower than the actual fee. 

```solidity
    function _fillBuyOrder(
        OrderRequest calldata orderRequest,
        bytes32 orderId,
        OrderState memory orderState,
        uint256 fillAmount,
        uint256 receivedAmount
    ) internal virtual {
        FeeState memory feeState = _feeState[orderId];
        uint256 remainingOrder = orderState.remainingOrder - fillAmount;
        // If order is done, close order and transfer fees
        if (remainingOrder == 0) {
            _closeOrder(orderId, orderRequest.paymentToken, feeState.remainingPercentageFees + feeState.feesEarned);
        } else {
            // Otherwise accumulate fees for fill
            // Calculate fees
            uint256 collection = 0; 
            if (feeState.remainingPercentageFees > 0) {
                // fee = remainingPercentageFees * fillAmount / remainingOrder
                collection = PrbMath.mulDiv(feeState.remainingPercentageFees, fillAmount, orderState.remainingOrder);
            }
            // Update fee state
            if (collection > 0) {
                _feeState[orderId].remainingPercentageFees = feeState.remainingPercentageFees - collection;
                _feeState[orderId].feesEarned = feeState.feesEarned + collection;
            }
        }

        // Mint asset
        IMintBurn(orderRequest.assetToken).mint(orderRequest.recipient, receivedAmount);
    }

    /// @inheritdoc OrderProcessor
    function _cancelOrderAccounting(OrderRequest calldata orderRequest, bytes32 orderId, OrderState memory orderState)
        internal
        virtual
        override
    {
        FeeState memory feeState = _feeState[orderId];
        // If no fills, then full refund
        // This addition is required to check for any fills
        uint256 refund = orderState.remainingOrder + feeState.remainingPercentageFees;
        // If any fills, then orderState.remainingOrder would not be large enough to satisfy this condition
        // feesEarned is always needed to recover flat fee
        if (refund + feeState.feesEarned == orderRequest.quantityIn) {
            _closeOrder(orderId, orderRequest.paymentToken, 0);
            // Refund full payment
            refund = orderRequest.quantityIn;
        } else {
            // Otherwise close order and transfer fees
            _closeOrder(orderId, orderRequest.paymentToken, feeState.feesEarned);
        }

        // Return escrow
        IERC20(orderRequest.paymentToken).safeTransfer(orderRequest.recipient, refund);
    }
```
For comparison, let's see how BuyOrderIssuer is implemented. In BuyOrderIssuer, fee is divided into two parts: remainingPercentageFees and feesEarned.
When the order is done, the two parts will be added together, which will cancel out the accuracy error of the collection; However, if the order is canceled, only the feesEarned amount will be allocated, and the precision error still exists.

## Impact

BuyOrderIssuer / SellOrderProcessor feesEarned calculation has precision errors, every time the order is filled, there will be a rounding error in the fee calculation result.

## Code Snippet

- https://github.com/sherlock-audit/2023-06-dinari/blob/main/sbt-contracts/src/issuer/BuyOrderIssuer.sol#L161-L215
- https://github.com/sherlock-audit/2023-06-dinari/blob/main/sbt-contracts/src/issuer/SellOrderProcessor.sol#L92-L151

## Tool used

Manual Review

## Recommendation

The fee should be calculated finally instead of each time of the fill, which will reduce the accuracy error to only one time.
