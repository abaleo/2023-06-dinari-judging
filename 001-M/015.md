ArmedGoose

medium

# Cancelled orders are refunded to recipients instead of payers, some users might lose funds

## Summary
When an order is requested, funds are taken off the requester's balance. When the same order is cancelled, the balance is returned to the recipient instead of the payer. If the recipient is different from the requester and is incorrect, then cancelling the order may cause the requester to lose funds, if the recipient is not an address owned by the requester.

## Vulnerability Detail
If an order is placed on behalf of someone, which presumably is allowed since the recipient is not `msg.sender` but a choosable value, and e.g. the requester put a wrong recipient address, the funds are lost anyway, because on cancellation the funds will be returned to that address and not to the payer. Inputting a wrong address may be a reason an user wants to cancel the order, but once its created, then the funds will be transferred to that recipient anyway. 

## Impact
Users who created orders with wrong recipients or simply unowned addresses as recipients will lose their funds as these funds will be returned to the recipients and not to those who paid for the order.

## Code Snippet
https://github.com/sherlock-audit/2023-06-dinari/blob/main/sbt-contracts/src/issuer/SellOrderProcessor.sol#L150
https://github.com/sherlock-audit/2023-06-dinari/blob/main/sbt-contracts/src/issuer/BuyOrderIssuer.sol#L214

Foundry poc:
Run `forge test --match-test testRequestCancelWrongAddr -vv`
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.19;

import "forge-std/Test.sol";
import "solady/test/utils/mocks/MockERC20.sol";
import "openzeppelin-contracts/contracts/proxy/ERC1967/ERC1967Proxy.sol";
import "./utils/mocks/MockBridgedERC20.sol";
import "./utils/SigUtils.sol";
import "../src/issuer/BuyOrderIssuer.sol";
import "../src/issuer/IOrderBridge.sol";
import {OrderFees, IOrderFees} from "../src/issuer/OrderFees.sol";
import "openzeppelin-contracts/contracts/utils/Strings.sol";

contract BuyOrderIssuerTest is Test {
    event TreasurySet(address indexed treasury);
    event OrderFeesSet(IOrderFees indexed orderFees);
    event OrdersPaused(bool paused);

    event OrderRequested(bytes32 indexed id, address indexed recipient, IOrderBridge.Order order, bytes32 salt);
    event OrderFill(bytes32 indexed id, address indexed recipient, uint256 fillAmount, uint256 receivedAmount);
    event OrderFulfilled(bytes32 indexed id, address indexed recipient);
    event CancelRequested(bytes32 indexed id, address indexed recipient);
    event OrderCancelled(bytes32 indexed id, address indexed recipient, string reason);

    error DuplicateOrder();

    BridgedERC20 token;
    OrderFees orderFees;
    BuyOrderIssuer issuer;
    MockERC20 paymentToken;
    SigUtils sigUtils;

    uint256 userPrivateKey;
    address user;
    address secondUser;

    address constant operator = address(3);
    address constant treasury = address(4);

    bytes32 constant salt = 0x0000000000000000000000000000000000000000000000000000000000000001;
    OrderProcessor.OrderRequest dummyOrder;
    
    uint256 dummyOrderFees;
    IOrderBridge.Order dummyOrderBridgeData;

    function setUp() public {
        userPrivateKey = 0x01;
        user = vm.addr(userPrivateKey);
        secondUser = makeAddr("secondUser");

        token = new MockBridgedERC20();
        paymentToken = new MockERC20("Money", "$", 6);
        sigUtils = new SigUtils(paymentToken.DOMAIN_SEPARATOR());

        orderFees = new OrderFees(address(this), 1 ether, 0.005 ether);

        BuyOrderIssuer issuerImpl = new BuyOrderIssuer();
        issuer = BuyOrderIssuer(
            address(
                new ERC1967Proxy(address(issuerImpl), abi.encodeCall(issuerImpl.initialize, (address(this), treasury, orderFees)))
            )
        );

        token.grantRole(token.MINTER_ROLE(), address(this));
        token.grantRole(token.MINTER_ROLE(), address(issuer));

        issuer.grantRole(issuer.PAYMENTTOKEN_ROLE(), address(paymentToken));
        issuer.grantRole(issuer.ASSETTOKEN_ROLE(), address(token));
        issuer.grantRole(issuer.OPERATOR_ROLE(), operator);

        dummyOrder = OrderProcessor.OrderRequest({
            recipient: user,
            assetToken: address(token),
            paymentToken: address(paymentToken),
            quantityIn: 100 ether,
            price: 0
        });
        (uint256 flatFee, uint256 percentageFee) =
            issuer.getFeesForOrder(dummyOrder.paymentToken, dummyOrder.quantityIn);
        dummyOrderFees = flatFee + percentageFee;
        dummyOrderBridgeData = IOrderBridge.Order({
            recipient: user,
            assetToken: address(token),
            paymentToken: address(paymentToken),
            sell: false,
            orderType: IOrderBridge.OrderType.MARKET,
            assetTokenQuantity: 0,
            paymentTokenQuantity: dummyOrder.quantityIn - dummyOrderFees,
            price: 0,
            tif: IOrderBridge.TIF.GTC,
            fee: dummyOrderFees
        });
    }

    function testRequestCancelWrongAddr() public {
        paymentToken.mint(user, dummyOrder.quantityIn);
        paymentToken.mint(secondUser, dummyOrder.quantityIn);

        vm.prank(user);
        paymentToken.increaseAllowance(address(issuer), dummyOrder.quantityIn);

        vm.prank(secondUser);
        paymentToken.increaseAllowance(address(issuer), dummyOrder.quantityIn);
        
        uint256 secondUserBalanceBefore = paymentToken.balanceOf(secondUser);
        console.log("secondUser balance before order: ", secondUserBalanceBefore);

        uint256 userBalanceBefore = paymentToken.balanceOf(user);
        console.log("user balance before order: ", userBalanceBefore);

        vm.prank(secondUser);
        issuer.requestOrder(dummyOrder, salt);

        uint256 secondUserBalanceAfter = paymentToken.balanceOf(secondUser);
        console.log("secondUser balance after order: ", secondUserBalanceAfter);


        bytes32 orderId = issuer.getOrderIdFromOrderRequest(dummyOrder, salt);
        vm.expectEmit(true, true, true, true);
        emit CancelRequested(orderId, user);
        vm.prank(secondUser);
        issuer.requestCancel(dummyOrder, salt);

        string memory reason = "User requested";
        vm.prank(operator);
        issuer.cancelOrder(dummyOrder, salt, reason);

        uint256 secondUserBalanceAfterCancel = paymentToken.balanceOf(secondUser);
        console.log("secondUser balance after cancellation succeeded: ", secondUserBalanceAfterCancel);

        uint256 userBalanceAfterCancel = paymentToken.balanceOf(user);
        console.log("user balance after cancellation succeeded: ", userBalanceAfterCancel);
    }

}
```

```text
Logs:
  secondUser balance before order:  100000000000000000000
  user balance before order:  100000000000000000000
  secondUser balance after order:  0
  secondUser balance after cancellation succeeded:  0
  user balance after cancellation succeeded:  200000000000000000000
```

## Tool used

Manual Review
Foundry

## Recommendation
Return the funds to the `orderState.requester` in the same way as it's checked if that address can cancel the order.
