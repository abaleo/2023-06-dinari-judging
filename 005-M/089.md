bin2chen

medium

# takeEscrow() possible take funds from closed orders

## Summary
When an order is closed `getOrderEscrow[orderId]` does not clear to 0.
Also `takeEscrow() ` does not determine if the order is closed or not
So in the case of concurrency, it may happen that trusted `OPERATOR` still takes the funds after the order is closed

## Vulnerability Detail

When the order is closed, only `getOrderEscrow[orderId]==remainingOrder` is ensured, but `getOrderEscrow[orderId]` is not cleared to 0
```solidity
contract DirectBuyIssuer is BuyOrderIssuer {
....
    function _cancelOrderAccounting(OrderRequest calldata order, bytes32 orderId, OrderState memory orderState)
        internal
        virtual
        override
    {
        // Prohibit cancel if escrowed payment has been taken and not returned or filled
        uint256 escrow = getOrderEscrow[orderId];
        if (orderState.remainingOrder != escrow) revert UnreturnedEscrow();

        // Standard buy order accounting
        super._cancelOrderAccounting(order, orderId, orderState);
    }
```

Also in `takeEscrow()` there is no order status determination, as long as `getOrderEscrow[orderId]` has a value it can be taken

```solidity
    function takeEscrow(OrderRequest calldata orderRequest, bytes32 salt, uint256 amount)
        external
        onlyRole(OPERATOR_ROLE)
    {
        // No nonsense
        if (amount == 0) revert ZeroValue();
        // Can't take more than escrowed
        bytes32 orderId = getOrderIdFromOrderRequest(orderRequest, salt);

        uint256 escrow = getOrderEscrow[orderId];
        if (amount > escrow) revert AmountTooLarge();

        // Update escrow tracking
        getOrderEscrow[orderId] = escrow - amount;
        // Notify escrow taken
        emit EscrowTaken(orderId, orderRequest.recipient, amount);

        // Take escrowed payment
        IERC20(orderRequest.paymentToken).safeTransfer(msg.sender, amount);
    }
```

The protocol is trusted for `OPERATOR_ROLE`
But in case of concurrency, the trusted `OPERATOR_ROLE` may still take the amount and fail to `returnEscrow()`
Example.
There are two `OPERATOR_ROLE`, which may both be `bot` or one bot concurrent processing tasks

1. `OPERATOR_ROLE_takeEscrow` is responsible for collecting `Escrow` and handling other tasks
2. `OPERATOR_ROLE_CANCEL` is responsible for handling order cancellation requests
3. when `OPERATOR_ROLE_takeEscrow` submit the `takeEscrow()` transaction to the memorypool
4. `OPERATOR_ROLE_CANCEL` receives the order cancellation task and submits the `cancelOrder()` transaction, which takes precedence over step 3.
5. It is the turn of `takeEscrow()` to execute, the order has been canceled,but  the funds can still be taken at that time

So it is recommended that `takeEscrow()` add a restriction that the order cannot be closed




## Impact

`OPERATOR_ROLE` Taking funds from closed orders may lead to problems such as not having enough funds in the contract and other users not being able to get a refund

## Code Snippet

https://github.com/sherlock-audit/2023-06-dinari/blob/main/sbt-contracts/src/issuer/DirectBuyIssuer.sol#L53-L71

## Tool used

Manual Review

## Recommendation

```solidity
    function takeEscrow(OrderRequest calldata orderRequest, bytes32 salt, uint256 amount)
        external
        onlyRole(OPERATOR_ROLE)
    {
        // No nonsense
        if (amount == 0) revert ZeroValue();
        // Can't take more than escrowed
        bytes32 orderId = getOrderIdFromOrderRequest(orderRequest, salt);
+      require(isOrderActive(orderId),"invalid order");
....
```
